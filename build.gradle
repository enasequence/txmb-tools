plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'uk.ac.ebi.ena.txmbtools'
version = '2.0.0'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    maven { url "https://gitlab.ebi.ac.uk/api/v4/groups/enasequence/-/packages/maven" }
    mavenCentral()
}

dependencies {
    api( group: 'uk.ac.ebi.ena.webin-cli', name: 'webin-cli-validator', version: '2.+' )

    implementation( group: 'org.apache.commons', name: 'commons-csv', version: '1.7' )

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation( 'org.mockito:mockito-core:2.23.4' )
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Source-Compatibility': project.sourceCompatibility)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar { classifier "sources" }
        }
    }
    repositories {
        maven {
            // Project specific maven repository in Gitlab.
            url "https://gitlab.ebi.ac.uk/api/v4/projects/1784/packages/maven"
            // Developer token in Gitlab.
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = project.ext.properties.gitlab_private_token
            }
            authentication {
                header( HttpHeaderAuthentication )
            }
        }
    }
}
