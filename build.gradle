apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'uk.ac.ebi.ena.txmbtools'
ext.version_base = '1.1.0'
version = version_base

ext.gitlab_project_id ='1784'

sourceCompatibility = 1.8
targetCompatibility = 1.8

wrapper {
    gradleVersion = '5.2.1'
}

repositories {
    mavenLocal()
    maven { url "https://gitlab.ebi.ac.uk/api/v4/groups/enasequence/-/packages/maven" }
    // For BioSamples client
    maven {
        url 'https://gitlab.ebi.ac.uk/api/v4/projects/2669/packages/maven'

        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = biosamples_gitlab_private_token
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
    mavenCentral()
}

dependencies {
    api( group: 'uk.ac.ebi.ena.webin-cli', name: 'webin-cli-validator', version: '1.+' )

    implementation( group: 'org.apache.commons', name: 'commons-csv', version: '1.7' )

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation( 'org.mockito:mockito-core:2.23.4' )
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

jar {
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        exclude("**/*.RSA")
        exclude("**/*.SF")
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Source-Compatibility': project.sourceCompatibility)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar { classifier "sources" }
        }
    }
    repositories {
        maven {
            // Project specific maven repository in Gitlab.
            url "https://gitlab.ebi.ac.uk/api/v4/projects/${gitlab_project_id}/packages/maven"
            // Developer token in Gitlab.
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = project.ext.properties.gitlab_private_token
            }
            authentication {
                header( HttpHeaderAuthentication )
            }
        }
    }
}
